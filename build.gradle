buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "org.jfrog.buildinfo:build-info-extractor-gradle:2.2.3"
  }
}

apply plugin: "groovy"

repositories {
  jcenter()
  mavenCentral()
}

configurations {
  asciidoclet
}

version = "1.0-SNAPSHOT"
group = "co.freeside.jdbi-time"

ext {
  isSnapshot = version.endsWith("-SNAPSHOT")
  versions = [
    groovy: "2.3.3",
    h2    : "1.4.178",
    jdbi  : "2.55",
    spock : "0.7-groovy-2.0"
  ]
}

dependencies {
  compile "org.jdbi:jdbi:$versions.jdbi"
  testCompile "org.codehaus.groovy:groovy-all:$versions.groovy:indy"
  testCompile "org.spockframework:spock-core:$versions.spock"
  testRuntime "com.h2database:h2:$versions.h2"
  asciidoclet "org.asciidoctor:asciidoclet:0.+"
}

plugins.withType(JavaPlugin) {
  sourceCompatibility = "1.8"
  targetCompatibility = "1.8"
}

tasks.withType(GroovyCompile) {
  groovyOptions.optimizationOptions.indy = true
}

configurations.all {
  resolutionStrategy {
    force "org.codehaus.groovy:groovy-all:$versions.groovy:indy"
  }
}

javadoc {
  options.docletpath = configurations.asciidoclet.files.asType(List)
  options.doclet = "org.asciidoctor.Asciidoclet"
}

apply from: "${rootDir}/gradle/publish.gradle"
